buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

defaultTasks 'info'

group = 'pl.edu.icm.pcj'

ext.versionNumber = "5.0"
ext.buildNumber = "SNAPSHOT"
try {
    ext.repoId = org.ajoberstar.grgit.Grgit.open(project.file('.')).head().abbreviatedId;
} catch (Exception e) {
    ext.repoId = "unknown"
}

repositories {
    //    mavenCentral()
}

dependencies {
    //    testCompile 'junit:junit:4.11'
}

compileJava {
    options.compilerArgs = [
            '-source', '1.8',
            '-target', '1.8',
            '-profile', 'compact3',
            '-Xlint:unchecked'
    ]
}

compileTestJava {
    options.compilerArgs += '-proc:none' // 'org.pcj.internal.StorageAnnotationProcessor'
}

task info {
    println "PCJ -- Parallel Computing in Java build script"
}

task javadoc(overwrite: true, type: Javadoc) {
    failOnError = false
    source = sourceSets.main.allJava
    exclude "org/pcj/internal/**"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task releaseBuild {
    outputs.upToDateWhen { false }
    releaseBuild.dependsOn(clean, jar, javadocJar, sourcesJar, publish)
}

gradle.taskGraph.whenReady { taskGraph ->
    def Properties properties = new Properties()

    def versionFile = file('build.version')
    if (versionFile.canRead()) {
        properties.load(new FileInputStream(versionFile))
    }

    if (properties['build.number'] == null) {
        properties['build.number'] = '0'
    }

    buildNumber = properties['build.number'].toInteger()

    if (taskGraph.hasTask(releaseBuild)) {
        properties['build.number'] = (buildNumber + 1).toString()
        properties.store(versionFile.newWriter(), null)
    } else {
        buildNumber = "$buildNumber-SNAPSHOT-$repoId"
    }
    jar.doFirst {
        version = "$versionNumber.$buildNumber"
        classifier = ''
        manifest {
            attributes('Implementation-Version': "$versionNumber.$buildNumber-$repoId")
        }
    }
    javadocJar.doFirst {
        version = "$versionNumber.$buildNumber"
    }
    sourcesJar.doFirst {
        version = "$versionNumber.$buildNumber"
    }

    def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
    def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
    version = "$versionNumber.$buildNumber"
    publishing.repositories.maven.url = version.contains('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
}

jar {
    from 'LICENSE'
    manifest {
        attributes('Implementation-Version': "$repoId",
                'Implementation-Vendor': System.getProperty('user.name'),
                'Implementation-Title': date,
                'Sealed': true,
                // 'Profile': 'compact3',
        )
        attributes(['Sealed': false], 'org/pcj/')
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd HH:mm:ss.SSS z')
    return formattedDate
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pcj'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'PCJ'
                description = 'PCJ is Java library for parallel computing in Java. It is based on the PGAS (Partitioned Global Address Space) paradigm. It allows for easy implementation in Java of any parallel algorithm. PCJ application can be run on laptop, workstation, cluster and HPC system including large supercomputers.'
                url = 'http://pcj.icm.edu.pl'
                licenses {
                    license {
                        name = 'The 3-Clause BSD License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'faramir'
                        name = 'Marek Nowicki'
                        email = 'faramir@mat.umk.pl'
                    }
                }
                scm {
                    url = 'https://github.com/hpdcj/PCJ'
                    connection = 'scm:git:git://github.com/hpdcj/PCJ.git'
                    developerConnection = 'scm:git:git@github.com:hpdcj/PCJ.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = "$buildDir/repos/unknown"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


// Code needed for running Main classes from Task Packages in NetBeans
def createJavaExec = { String taskName ->
    project.task(taskName, dependsOn: project.testClasses, type: JavaExec) {
        def definedMainClass = project.hasProperty('mainClass') ? project.mainClass : ''
        if (definedMainClass == null) definedMainClass = ''
        definedMainClass = definedMainClass.toString()

        main = definedMainClass
        classpath = project.sourceSets.test.runtimeClasspath
        standardInput = System.in
        //jvmArgs = ["-Xmx3g","-Xms3g"]
        maxHeapSize = "2g"

    }
}

createJavaExec('run')
